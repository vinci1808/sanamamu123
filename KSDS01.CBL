 IDENTIFICATION DIVISION.
 PROGRAM-ID. KSDS01.
 ENVIRONMENT DIVISION.
 INPUT-OUTPUT SECTION.
* 
 FILE-CONTROL.
     SELECT EMPLOYEE-MASTER ASSIGN TO 'EMPMAST.DAT'
         ORGANIZATION IS INDEXED
         ACCESS MODE  IS RANDOM
         RECORD KEY   IS EMP-ID
         FILE STATUS  IS WS-EMP-STAT.
     SELECT INPUT-FILE ASSIGN TO 'INPUT.DAT'
         ORGANIZATION IS SEQUENTIAL
         ACCESS MODE  IS SEQUENTIAL
         FILE STATUS  IS WS-INP-STAT.
*         
 DATA DIVISION.
 FILE SECTION.
 FD  EMPLOYEE-MASTER
     RECORD CONTAINS 50 CHARACTERS.
 01  MASTER-RECORD.              
     03  EMP-ID       PIC X(05).   
     03  EMP-NAME     PIC X(15).   
     03  EMP-LOC      PIC X(03).   
     03  EMP-DOB      PIC X(08).   
     03  EMP-TECH     PIC X(05).   
     03  EMP-EARN     PIC 9(05)V99.
     03  EMP-DEDN     PIC 9(05)V99.     

 FD  INPUT-FILE     
     RECORD CONTAINS 5 CHARACTERS
     RECORDING MODE IS F.
 01  INPUT-RECORD     PIC X(05).
* 
 WORKING-STORAGE SECTION.
 01  WS-VARIABLES.
     03  WS-FILE-FLAG     PIC X(01) VALUE 'N'.
         88  END-OF-FILE            VALUE 'Y'.
     03  WS-EMP-FLAG      PIC X(01) VALUE 'N'.
         88  EMP-FOUND              VALUE 'Y'.
     03  EMP-VALID        PIC 9(02) VALUE ZERO.
     03  EMP-INVALID      PIC 9(02) VALUE ZERO.
     03  TOT-RECS         PIC 9(02) VALUE ZERO.
     03  WS-EMP-STAT      PIC X(02) VALUE SPACES.
     03  WS-INP-STAT      PIC X(02) VALUE SPACES.
* 
 PROCEDURE DIVISION.
 0000-MAIN-PARA.
     PERFORM 1000-INIT-PARA.
     PERFORM 2000-PROCESS-PARA  UNTIL  END-OF-FILE
     PERFORM 9000-END-PARA
     STOP RUN.
* 
 1000-INIT-PARA.
     OPEN INPUT EMPLOYEE-MASTER
     DISPLAY 'EMP OPEN FS ', WS-EMP-STAT
     OPEN INPUT INPUT-FILE.
     DISPLAY 'REP OPEN FS ', WS-INP-STAT
     PERFORM 1500-READ-PARA.
* 
 1500-READ-PARA.
     READ INPUT-FILE
        AT END
           MOVE 'Y' TO WS-FILE-FLAG
        NOT AT END
           ADD 1 TO TOT-RECS
     END-READ.
*
 2000-PROCESS-PARA.
     PERFORM 2500-READ-MASTER
     IF EMP-FOUND
         DISPLAY 'EMP=> ',  MASTER-RECORD
         ADD 1 TO EMP-VALID
     ELSE
         DISPLAY 'INVALID EMPLOYEE ID ', INPUT-RECORD
         ADD 1 TO EMP-INVALID
     END-IF
     PERFORM 1500-READ-PARA.
*
 2500-READ-MASTER.
     MOVE INPUT-RECORD TO EMP-ID
     READ EMPLOYEE-MASTER
         INVALID KEY
             MOVE 'N' TO WS-EMP-FLAG
         NOT INVALID KEY
             MOVE 'Y' TO WS-EMP-FLAG
     END-READ.
* 
 9000-END-PARA.
     DISPLAY '==================================='
     DISPLAY 'TOTAL RECORDS   = ', TOT-RECS
     DISPLAY 'VALID EMP IDS   = ', EMP-VALID
     DISPLAY 'INVALID EMP IDS = ', EMP-INVALID
     CLOSE EMPLOYEE-MASTER, INPUT-FILE.
*
